_default:
    @just --list

clean:
    rm -rf build sim_build

# Assemble a file for the processor
assemble file:
    mkdir -p build
    riscv32-unknown-elf-as -march=rv32i -o build/program.o {{file}}
    riscv32-unknown-elf-ld -m elf32lriscv -T linker.ld -o build/program.elf build/program.o

simulate: && gui
    mkdir -p build/sim
    riscv32-unknown-elf-objcopy -O binary build/program.elf build/program.bin
    hexdump -v -e '1/4 "%08x\n"' build/program.bin > build/program.hex
    uv run runner.py build/program.hex

gui:       
    surfer -s cpu.surf.ron build/sim/out.ghw

compile file:
    mkdir -p build
    riscv32-unknown-elf-gcc -mabi=ilp32 -march=rv32i -nostdlib -nostartfiles -T linker.ld -o build/program.elf {{file}}

[no-cd]
rust path: && simulate
    mkdir -p ../build
    riscv32-unknown-elf-objdump -d -h {{path}} | head -n 100
    cp {{path}} ../build/program.elf